#include "../lib/ShaderAPI.h"
#include "../lib/Color.h"
#include "../lib/Variation.h"

shader apLobeSpecular_1
[[ string help = "apettit's prefered inputs for pxrSurface's specular lobe" ]]
(
    float inputKFace = 1.0
    [[ string label = "KFace" ]],
    float inputKEdge = 1.0
    [[ string label = "KEdge" ]],
    float roughness = 0.2
    [[ string label = "Roughness"]],
    color inputTint = color(1.0)
    [[ string label = "Tint" ]],

    VARIATION_INPUTS(Specular),
    int kEdgeVar = 0
    [[
        int connectable = 0,
        string help = "Have variation affect the edge value as well",
        string label = "Affect Edge",
        string page = "Specular Variation",
        string widget = "checkBox"
    ]],
    VARIATION_INPUTS(Roughness),

    output color resultKFace = color(0.0),
    output color resultKEdge = color(0.0),
    output float resultRoughness = 0.0
)
{
    float kFace = inputKFace;
    float kEdge = inputKEdge;

    kFace = VARIATION_SET(inputKFace, Specular);

    if (kEdgeVar)
        kEdge = VARIATION_SET(inputKEdge, Specular);

    resultKFace = color(kFace) * inputTint;
    resultKEdge = color(kEdge) * inputTint;
    resultRoughness = VARIATION_SET(roughness, Roughness);
}